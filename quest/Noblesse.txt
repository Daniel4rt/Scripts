prt_in3,111,74,5	script	Oliver#nob	626,{
	if(Q_NOBLE == 2) {
		npctalk "Zzzz... ZzZz...","Oliver#nob",bc_self;
		emotion ET_SLEEPY;
		end;
	}
	mes "[^777777Oliver^000000]";
	if(Q_NOBLE == 1) {
		if(BaseLevel < 100) {
			cutin "bu_oliver2",4;
			mes "Sigue esforzándote para que algún otro día vengas a contarme más interesantes historias.";
			mes "Algo como en ^ff0000Nv. 100^000000.";
			close3;
		}
		cutin "bu_oliver1",2;
		mes "Veo que has vuelto con más y nuevas historias que contarme.";
		next;
		select("Contarle nuevas historias a Oliver.");
		message strcharinfo(0),"Le cuentas tus nuevas historias a Oliver...";
		progressbar "008800",5;
		cutin "bu_oliver2",2;
		mes "[^777777Oliver^000000]";
		mes "Fantástico, símplemente fantástico.";
		next;
		cutin "bu_oliver3",2;
		mes "[^777777Oliver^000000]";
		mes "Bueno, como te he prometido, aquí tienes tu nueva habilidad, úsala para apoyar a los demás a conseguir sus objetivos.";
		next;
		dispbottom " * Has obtenido la habilidad 'Noblesse Blessing'.",0x000000;
		skill 3045,1,3;
		cutin "bu_oliver6",2;
		mes "[^777777Oliver^000000]";
		mes "Bueno, yo iré a tirarme una siesta por un siglo en estos cómodos sofá.";
		close2;
		cutin "",255;
		Q_NOBLE = 2;
		end;
	}
	cutin "bu_oliver1",2;
	if(BaseLevel < 185) {
		mes "Estos sofá se miran tan cómodos.";
		next;
		cutin "bu_oliver2",2;
		mes "[^777777Oliver^000000]";
		mes "¿Eh?, ¿qué quieres?";
		mes "No me apetece hablar con alguien que no tiene aventuras para contarme.";
		next;
		cutin "bu_oliver5",2;
		mes "[^777777Oliver^000000]";
		mes "Piérdete.";
		close3;
	}
	mes "Oh, hola, ¿qué necesitas?";
	next;
	select("Contarle la historia de mi vida.");
	message strcharinfo(0),"Le cuentas tu historia a Oliver...";
	progressbar "008800",5;
	cutin "bu_oliver4",2;
	mes "[^777777Oliver^000000]";
	mes "Ya veo, que interesante ha sido tu vida como aventurero, sin duda eres merecedor de un gran premio.";
	next;
	cutin "bu_oliver3",2;
	mes "[^777777Oliver^000000]";
	mes "Pero, ¿sabes qué?";
	mes "Sería más interesante tu vida si la comienzas nuevamente, ¿qué dices?";
	next;
	cutin "bu_oliver4",2;
	mes "[^777777Oliver^000000]";
	mes "Te daré la bendición de los ^5533ffnobles^000000 a cambio de que inicies tu vida nuevamente.";
	next;
	switch(select("Me lo pensaré:^5533ffIniciar mi vida nuevamente^000000:^777777Salir^000000")) {
		case 1:
			cutin "bu_oliver6",2;
			mes "[^777777Oliver^000000]";
			mes "No te preocupes, aquí seguiré si cambias de opinión.";
			close3;
		case 2:
			cutin "bu_oliver7",2;
			mes "[^777777Oliver^000000]";
			mes "¿Estás segur"+(Sex?"o":"a")+"?,";
			mes "volverás a iniciar tus aventuras como un ^ff0000"+callsub(S_GetName)+"^000000";
			next;
			if(select("Vamos allá:No, gracias") == 2) {
				cutin "bu_oliver6",2;
				mes "[^777777Oliver^000000]";
				mes "Regresa cuando quieras.";
				close3;
			}
			cutin "bu_oliver4",2;
			mes "[^777777Oliver^000000]";
			if (checkfalcon() || checkcart() || checkriding() || ismounting()) {
				mes "Por favor remueve tu ^5533ff" +
					((checkfalcon()) ? "halcón" : "") +
					((checkcart()) ? "carro" : "") +
					((checkriding()) ? "Peco Peco" : "") +
					((ismounting()) ? "montura" : "") +
					"^000000 antes de proceder.";
				close3;
			}
			if (SkillPoint) {
				mes "Por favor utiliza tus puntos de habilidad antes de proceder.";
				close3;
			}
			.@to_cls = callsub(S_GetJob);
			if(.@to_cls == -1) {
				mes "Lo siento pero no puedo obtener información sobre el inicio de tus aventuras.";
				close3;
			}
			if(.@to_cls == Job_Novice)
				lastNobless = Class;
			jobchange .@to_cls;
			resetlvl(1);
			resetstatus;
			resetskill;
			SkillPoint = 0;
			specialeffect2 EF_ANGEL2;
			specialeffect2 EF_ELECTRIC;
			mes "Listo, ahora me gustaría escuchar nuevas aventuras, vuelve cuando hayas obtenido más historias para contarme, y te recompensaré con la ^5533ffBendición de los Nobles^000000.";
			close2;
			cutin "",255;
			Q_NOBLE = 1;
			end;
	}
S_GetName:
	if( Class == Job_Summoner ) {
		if( eaclass()&EAJL_BABY )
			return "Baby Summoner";
		return "Summoner";
	}
	if( eaclass()&EAJL_UPPER )
		return "High Novice";
	if( roclass(eaclass()|EAJL_UPPER) < 0 )
		return "Novice";
	if( eaclass()&EAJL_BABY )
		return "Baby Novice";
	return "Desconocido";
S_GetJob:
	if( Class == Job_Summoner ) {
		if( eaclass()&EAJL_BABY )
			return Job_Baby_Summoner;
		return Job_Summoner;
	}
	if( eaclass()&EAJL_UPPER )
		return Job_Novice_High;
	if( roclass(eaclass()|EAJL_UPPER) < 0 )
		return Job_Novice;
	if( eaclass()&EAJL_BABY )
		return Job_Baby;
	return -1;
}