//      ______  __    __                                 
//     /\  _  \/\ \__/\ \                                
//     \ \ \_\ \ \ ,_\ \ \___      __    ___      __     
// _____\ \  __ \ \ \/\ \  _ `\  /'__`\/' _ `\  /'__`\   
///\  __\\ \ \/\ \ \ \_\ \ \ \ \/\  __//\ \/\ \/\ \_\.\_ 
//\ \_\_/ \ \_\ \_\ \__\\ \_\ \_\ \____\ \_\ \_\ \__/.\_\
// \/_/    \/_/\/_/\/__/ \/_/\/_/\/____/\/_/\/_/\/__/\/_/
//
//========================================================
// ---                Last-Man Standing                ---
//========================================================
// ---                   by DanielArt                  ---
//========================================================
lastman,70,40,4	script	#LASTMAN	111,10,10,{
OnTouch:
OnPCKillEvent:
	.@m = getmapusers("lastman");
	sleep2 100;
	if(strcharinfo(3) == "lastman" && .@m < 2 && $LASTMAN == 1) {
		announce "¡"+strcharinfo(0)+" ha ganado el evento de Last-Man Standing!",bc_all,0xFFFFAA;
		F_Event 1,rand(1,6);
		$LASTMAN = 0;
		sleep2 5000;
		warp "prontera",150,180;
		end;
	}
	end;
OnPCDieEvent:
OnPCLoginEvent:
	if(strcharinfo(3) == "lastman")
		warp "prontera",150,180;
	end;
OnLastMan:
	$LASTMAN = 0;
	removemapflag "lastman",mf_pvp;
	removemapflag "lastman",mf_pvp_noparty;
	removemapflag "lastman",mf_pvp_noguild;
	donpcevent "last_e::OnEnable";
	announce "¡El Evento de Last-Man Standing comenzará en 2 minutos!",bc_all,0xFFFFAA;
	sleep2 5000;
	announce "¡Combate con todo el mundo por ser el último sobreviviente y gana un fabuloso premio!",bc_all,0xFFFFAA;
	sleep2 5000;
	announce "Ve a la Ciudad de Prontera para poder participar.",bc_all,0xFFFFAA;
	sleep2 60000;
	announce "¡El Evento de Last-Man Standing comenzará en 1 minuto!",bc_all,0xFFFFAA;
	sleep2 50000;
	announce "¡El Evento de Last-Man Standing comenzará en 10 segundos!",bc_all,0xFFFFAA;
	sleep2 11000;
	donpcevent "last_e::OnDisable";
	announce "¡El Evento de Last-Man Standing ha comenzado!",bc_all,0xFFFFAA;
	end;
}
prontera,190,177,4	script	Last-Man Standing::last_e	623,{
	mes "[ ^008800Last-Man Standing^000000 ]";
	mes "¿Listo para el combate?";
	next;
	switch(select("Información:^008800Participar^000000 [ "+getmapusers("lastman")+" ]:^777777Salir^000000")) {
		case 1:
			mes "[ ^008800Last-Man Standing^000000 ]";
			mes "Entrarás a un mapa junto con el resto de los usuarios participantes.";
			next;
			mes "[ ^008800Last-Man Standing^000000 ]";
			mes "Dentro, tendrás que buscar la manera de sobrevivir al resto de los usuarios.";
			close;
		case 2:
			warp "lastman",0,0;
			close;
		case 3:
			mes "[ ^008800Last-Man Standing^000000 ]";
			mes "Vuelve cuando quieras participar.";
			close;
	}
OnEnable:
	enablenpc "last_e";
	movenpc "last_e",156,179;
	waitingroom "Last-Man Stand",0;
	end;
OnDisable:
	movenpc "last_e",190,177;
	disablenpc "last_e";
	delwaitingroom;
	mapannounce "lastman","¡Prepárate, que el evento está a punto de empezar!",bc_map,0xFFFFAA;
	sleep2 5000;
	mapannounce "lastman","¡CUENTA REGRESIVA!.",bc_map,0xFFFFAA;
	soundeffectall "s4\\new_round_in.wav",0,"lastman",0,0,300,300;
	sleep2 1000;
	mapannounce "lastman","¡5!.",bc_map,0xFFFFAA;
	soundeffectall "s4\\5.wav",0,"lastman",0,0,300,300;
	sleep2 1000;
	mapannounce "lastman","¡4!.",bc_map,0xFFFFAA;
	soundeffectall "s4\\4.wav",0,"lastman",0,0,300,300;
	sleep2 1000;
	mapannounce "lastman","¡3!.",bc_map,0xFFFFAA;
	soundeffectall "s4\\3.wav",0,"lastman",0,0,300,300;
	sleep2 1000;
	mapannounce "lastman","¡2!.",bc_map,0xFFFFAA;
	soundeffectall "s4\\2.wav",0,"lastman",0,0,300,300;
	sleep2 1000;
	mapannounce "lastman","¡1!.",bc_map,0xFFFFAA;
	soundeffectall "s4\\1.wav",0,"lastman",0,0,300,300;
	sleep2 1000;
	if(getmapusers("lastman") == 1 || getmapusers("lastman") == 0) {
		mapannounce "lastman","No hay suficientes usuarios en el mapa.",bc_map,0xFFFFAA;
		sleep2 5000;
		mapwarp "lastman","prontera",150,180;
		end;
	}
	$LASTMAN = 1;
	mapannounce "lastman","¡PELEEN!.",bc_map,0xFFFFAA;
	soundeffectall "s4\\ready2.wav",0,"lastman",0,0,300,300;
	setmapflag "lastman",mf_pvp;
	setmapflag "lastman",mf_pvp_noparty;
	setmapflag "lastman",mf_pvp_noguild;
	end;
OnInit:
	disablenpc "last_e";
	delwaitingroom;
	end;
}
lastman	mapflag	nopenalty
lastman	mapflag	nozenypenalty
lastman	mapflag	nobranch
lastman	mapflag	notrade
lastman	mapflag	clouds
lastman	mapflag	nomemo
lastman	mapflag	noteleport
lastman	mapflag	nowarp
lastman	mapflag	noreturn
lastman	mapflag	restricted	3
lastman	mapflag	nosave	SavePoint
lastman	mapflag	nowarpto